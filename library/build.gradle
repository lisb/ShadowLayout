/*
 * Copyright (C) 2015 Basil Miller
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace "com.gigamole.library"

    compileSdk rootProject.tools.compileSdk

    defaultConfig {
        minSdk rootProject.tools.minSdk
        targetSdk rootProject.tools.targetSdk
        versionCode rootProject.tools.versionCode
        versionName "${rootProject.tools.versionName}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        publishing {
            singleVariant("release") {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'androidx.appcompat:appcompat:1.7.0'
    api 'org.jsoup:jsoup:1.18.1'
}

afterEvaluate {
    def localProps = new Properties()
    localProps.load(rootProject.file('local.properties').newDataInputStream())

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/ShadowLayout"
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.lisb.android'
                artifactId = 'shadowlayout'
                version = "${rootProject.tools.versionName}"
            }
        }
    }
}